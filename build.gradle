println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

repositories {
    maven {
        url "$projectDir/../CommonMaven"
    }
    maven { // Patchouli + JEI
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name "gigaherz's things"
        url 'https://dogforce-games.com/maven'
    }
    maven {
        name = "thiakil"
        url = "https://maven.thiakil.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name 'LDTTeam for Aequivaleo'
        url 'https://ldtteam.jfrog.io/ldtteam/modding'
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    flatDir {
        dirs 'deps'
    }
    mavenLocal()
}

version = "0.16.0-alpha"
group = "dev.gigaherz.elementsofpower" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ElementsOfPower-UNKNOWN"

project.afterEvaluate {
    archivesBaseName = "ElementsOfPower-${project.MC_VERSION}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

minecraft {
    //mappings channel: 'official', version: '1.19.2'
    mappings channel: 'parchment', version: '1.19.3-2023.03.12-1.20.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            forceExit false

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        client_nocache {
            parent runs.client

            property 'elementsOfPower.disableCaches', 'true'
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            forceExit false

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            forceExit false

            args '--mod', 'elementsofpower', '--all', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'

    compileOnly fg.deobf("mezz.jei:jei-1.20.1-common-api:15.2.0.22")
    //runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-common:15.2.0.22")
    compileOnly fg.deobf("mezz.jei:jei-1.20.1-forge-api:15.2.0.22")
    //runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.2.0.22")

    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.2.0-beta.3+1.20.1")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:5.2.0-beta.3+1.20.1")

    compileOnly fg.deobf("com.ldtteam:aequivaleo:1.20.1-0.1.113-ALPHA:api")
    runtimeOnly fg.deobf("com.ldtteam:aequivaleo:1.20.1-0.1.113-ALPHA:universal")

    //runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22")

    implementation fg.deobf("dev.gigaherz.guidebook:Guidebook-1.20.1:3.8.0")

    //runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.2-47")
    //runtimeOnly fg.deobf("deps:Botania-1.16.3:409")
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
            "Specification-Title": "elementsofpower",
            "Specification-Vendor": "gigaherz",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"gigaherz",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

project.afterEvaluate {
    publish.dependsOn('reobfJar')
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId project.archivesBaseName
                from components.java

                //artifact sourceJar

                pom.withXml {
                    asNode().dependencies.dependency.each { dep ->
                        if (dep.groupId.last().value().last() in ["net.minecraftforge", "mezz.jei"]) {
                            assert dep.parent().remove(dep)
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url "$projectDir/../CommonMaven"
            }
        }
    }
}