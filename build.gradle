println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

buildscript {
    repositories {
        maven { url 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

repositories {
    maven {
        url "$projectDir/../CommonMaven"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url 'https://dogforce-games.com/maven'
    }
    maven {
        name = "thiakil"
        url = "https://maven.thiakil.com/"
    }
    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name 'LDTTeam for Aequivaleo'
        url 'https://ldtteam.jfrog.io/ldtteam/modding'
    }
    maven { // TOP
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
    maven { // Patchouli
        url 'https://maven.blamejared.com'
    }
    flatDir {
        dirs 'deps'
    }
    mavenLocal()
}

version = "0.13.1-alpha"
group = "gigaherz.elementsofpower" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "ElementsOfPower-UNKNOWN"

project.afterEvaluate {
    archivesBaseName = "ElementsOfPower-${project.MC_VERSION}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main.resources.srcDirs += 'src/main/generated_resources'
}

minecraft {
    //mappings channel: 'official', version: '1.18.1'
    mappings channel: 'parchment', version: '2021.12.13-nightly-SNAPSHOT-1.18.1'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.disableRefMap', 'true'

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        client_nocache {
            parent runs.client

            property 'elementsOfPower.disableCaches', 'true'
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.disableRefMap', 'true'

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', ''
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'elementsofpower', '--all', '--output', file('src/main/generated_resources/'), '--existing', file('src/main/resources/')

            mods {
                thismod {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.59'

    compileOnly fg.deobf("mezz.jei:jei-1.18.1:9.2.3.78:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.1:9.2.3.78")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.1-5.0.6.0")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:1.18.1-5.0.6.0:api")

    compileOnly fg.deobf("com.ldtteam:aequivaleo:1.18-0.1.89-ALPHA:api")
    //compileOnly fg.deobf("com.ldtteam:aequivaleo:1.18-0.1.89-ALPHA:vanilla-api")
    runtimeOnly fg.deobf("com.ldtteam:aequivaleo:1.18-0.1.89-ALPHA:universal")

    //runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:1.16-3.1.4-22")

    implementation fg.deobf("dev.gigaherz.guidebook:Guidebook-1.18:3.4.0")

    //runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:1.16.2-47")
    //runtimeOnly fg.deobf("deps:Botania-1.16.3:409")
}

jar {
    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
            "Specification-Title": "elementsofpower",
            "Specification-Vendor": "gigaherz",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"gigaherz",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.publish.dependsOn build
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName
            from components.java

            //artifact sourceJar
        }
    }

    repositories {
        maven {
            url "$projectDir/../CommonMaven"
        }
    }
}

